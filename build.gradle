buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id 'net.researchgate.release' version '2.1.2'
    id 'com.github.kt3k.coveralls' version '2.4.0'
}

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'idea'

apply from: 'libraries.gradle'
apply from: 'gradle/license.gradle'
apply from: 'gradle/release.gradle'

apply plugin: 'findbugs'
apply plugin: 'pmd'

apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.7

group = "com.github.jmnarloch"
archivesBaseName="feign-encoding-spring-cloud-starter"

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'feign-encoding-spring-cloud-starter',
                   'Implementation-Version': version
    }
}

repositories {
    jcenter()
}

compileJava {
    options.fork = true
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.netflix.feign') {
                details.useVersion "8.7.1"
            } else if(details.requested.group == 'org.apache.httpcomponents') {
                details.useVersion "4.4.1"
            }
        }
    }
}

dependencies {

    compile (libraries.springCloudFeign)
    compile (libraries.springBootConfigurationProcessor) {
        ext.optional = true
    }

    testCompile (libraries.springCloudRibbon)
    testCompile (libraries.feignCore)
    testCompile (libraries.feignHttpClient)
    testCompile (libraries.httpClient)
    testCompile (libraries.springBootWeb)
    testCompile (libraries.springBootTest)
    testCompile (libraries.junit)
    testCompile (libraries.mockito)
}

findbugs {
    ignoreFailures = true
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
